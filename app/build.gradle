apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'


android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "com.login.demo"
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ext.enableCrashlytics = true
            applicationVariants.all { variant ->
                renameAPK(variant)
            }
        }
        debug {
            minifyEnabled false
            ext.enableCrashlytics = true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    androidExtensions {
        experimental = true
    }

    dataBinding {
        enabled = true
    }

    flavorDimensions "default"
    productFlavors {
        stage {
            applicationId "com.login.demo.stage"
            versionCode 1
            versionName "1.0"
            resValue "string", "app_name", "LoginDemo-Stage"


        }
        dev {
            applicationId "com.login.demo.dev"
            versionCode 1
            versionName "1.0"
            resValue "string", "app_name", "LoginDemo-Dev"

        }
        prod {
            applicationId "com.login.demo"
            versionCode 1
            versionName "1.0"
            resValue "string", "app_name", "LoginDemo"

        }
    }

    lintOptions {
        lintConfig file("lint.xml")

        // if set to true (default), stops the build if errors are found.
        abortOnError true

        checkAllWarnings true

        // Turns off checks for the issue IDs you specify.
        disable 'TypographyFractions', 'TypographyQuotes'
        // Turns on checks for the issue IDs you specify. These checks are in
        // addition to the default lint checks.
        enable 'RtlHardcoded', 'RtlCompat', 'RtlEnabled'
        // To enable checks for only a subset of issue IDs and ignore all others,
        // list the issue IDs with the 'check' property instead. This property overrides
        // any issue IDs you enable or disable using the properties above.
        check 'NewApi', 'InlinedApi'
        // If set to true, turns off analysis progress reporting by lint.
        quiet true
        // if true, only report errors.
        ignoreWarnings false
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$androidXAppCompatVersion"
    implementation "androidx.core:core-ktx:$androidXVersion"
    implementation "com.google.android.material:material:$googleMaterialVersion"

    implementation "androidx.legacy:legacy-support-v4:$legacySupportVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleExtVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleViewmodelVersion"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycleVersion"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    testImplementation "junit:junit:$jUnitVersion"
    androidTestImplementation "androidx.test.ext:junit:$extJUnitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    implementation project(path: ':base')

    implementation "com.android.support:multidex:$multidexSupportVersion"

    /*sp and dp font size*/
    implementation "com.intuit.sdp:sdp-android:$intuitsspsdpVersion"
    implementation "com.intuit.ssp:ssp-android:$intuitsspsdpVersion"
    /*rxjava*/
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation 'androidx.room:room-rxjava2:2.0.0'
    /*koin*/
    implementation "org.koin:koin-core:$koinVersion"
    implementation "org.koin:koin-android:$koinVersion"
    implementation "org.koin:koin-androidx-viewmodel:$koinVersion"
    implementation "org.koin:koin-androidx-scope:$koinVersion"

    /*retrofit and okHttp*/
    implementation "com.squareup.retrofit2:retrofit:$retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitLibraryVersion"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:$retrofitCoroutineLibVersion"
    implementation "com.squareup.okhttp3:okhttp:$okHttpLibraryVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpLibraryVersion"
    implementation "com.jakewharton.timber:timber:$timberVersion"

    /*dependency for validate phone number*/
    implementation "io.michaelrocks:libphonenumber-android:$libphonenumberVersion"



    //dependency for custom progressbar
    implementation "com.wang.avi:library:$customProgressBarWangLibVersion"



}

static def renameAPK(variant) {
    variant.outputs.all { output ->
        def formattedDate = new Date().format('dd-MM-YYYY')
        def projectName = variant.mergedFlavor.resValues.get('app_name').getValue()
        def projectVersionName = "_v" + variant.productFlavors.get(0).versionName
        def projectBuildCode = "_b" + variant.productFlavors.get(0).versionCode
        def buildDate = "_d" + formattedDate
        def flavorsName = "_" + variant.productFlavors.get(0).name
        def flavorsType = "_" + variant.buildType.name
        def fileName = projectName + projectVersionName + projectBuildCode + buildDate + flavorsName + flavorsType + ".apk"
        outputFileName = fileName
    }
}
